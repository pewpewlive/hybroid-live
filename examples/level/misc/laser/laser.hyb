env Laser as Level

use Pewpew
use Fmath
use Globals
use ShipEntity

const LASER_DURATION = 16f
const HIT_COOLDOWN_MAX = 4
const LENGTH = 7000f

entity Laser {
    fixed timer = LASER_DURATION

    bool isDead
    number color 
    number hitCooldown = 0

    fixed lx, ly
    fixed slope
    fixed x, y
    fixed b2

    spawn(fixed x, y, lookToAngle, number color) {
        self.x, self.y = x, y
        self.color = color
        SetEntityMeshAngle(self, lookToAngle, 0f, 0f, 1f)
        SkipEntityMeshAttributesInterpolation(self)
        SetEntityMeshColor(self, color)
        ly, lx = Sincos(lookToAngle)
        if lx == 0f {
            slope = 2fx
        } else {
            slope = ly/lx
            b2 = -slope*x+y
        }
        SetEntityMeshXYZScale(self, LENGTH, 1f, 1f)
        SetEntityMesh(self, LaserMesh, 0)
        PlaySound(LaserSound, 0, x, y)
        SpawnEntity(self, 0)
    }

    destroy() {
        SetEntityUpdateCallback(self, fn(entity _){})
        isDead = true
        DestroyEntity(self)
    }

    Update() {
        hitCooldown -= 1
        timer -= 1f
        let t = FmathHelpers:Remap(1f, 0f, LASER_DURATION, 0f, timer)^2f
        SetEntityMeshXYZScale(self, LENGTH, t, 1f)
        SetEntityMeshColor(self, ColorHelpers:LerpColors(0x00000000, self.color, t))
        if timer <= 0f {
            destroy self()
            return
        }
        if !IsEntityAlive(SHIP.id) return

        let px, py = GetEntityPosition(SHIP.id)
        if IsHittable(px, py) {
            CollideWithPlayer()
        }

        for v in every ShardEntity:Shard {
            let ex, ey = GetEntityPosition(v)
            if IsHittable(ex, ey) {
                destroy v()
            }
        }
    }

    fn IsHittable(fixed ex, ey) -> bool {
        fixed dist
        if slope == 2fx {
            dist = Fmath:AbsFixed(x-ex)
        } else {
            let b1 = -slope*ex+ey
            dist = Fmath:AbsFixed(b1-b2)/Fmath:Sqrt(1f+slope^2f)
        }

        let playerVector = Fmath:Atan2(ey-y, ex-x)
        let laserVector = Fmath:Atan2(ly, lx)
        let diff = Fmath:AbsFixed(playerVector-laserVector)
        
        bool isInFront
        if diff < 90d {
            isInFront = true
        } else if diff > 270d {
            isInFront = true
        }

        return dist < FmathHelpers:Remap(3f, 18f, 0f, LASER_DURATION, timer) and timer > 1f and isInFront
    }

    fn CollideWithPlayer() {
        if !IsEntityAlive(SHIP.id) return
        if hitCooldown < 0 {
            DamageShip(SHIP.id, 1)
        }
        hitCooldown = HIT_COOLDOWN_MAX
    }
}