env Gameplay as Level

use Pewpew
use Fmath
use Globals
use ShipEntity

pub alias EntitySpawn = fn(number, number)

fn Rangle() -> fixed {
  return RandomFixed(0d, 360d)
}

const SAFE_AREA = 50f

pub fn PlayerSafeSpawn(fn() -> (fixed, fixed) f) -> (fixed, fixed) {
    let x, y = f()
    let px, py = GetEntityPosition(SHIP.id)
    let counter = 0
    while AbsFixed(x - px) < SAFE_AREA and AbsFixed(y - py) < SAFE_AREA {
        counter += 1
        x, y = f()
        if counter > 16 {
            return x, y
        }
    } 
    return x, y
}

pub fn RandomWallSpawn(fixed offset) -> (fixed, fixed) {
    return FmathHelpers:Clamp(ToFixed(RandomNumber(0, 1))*WIDTH, offset, WIDTH-offset), 
            FmathHelpers:Clamp(ToFixed(RandomNumber(0, 1))*HEIGHT, offset, HEIGHT-offset)
}

pub fn RandomSpawn() -> (fixed, fixed) {
    return RandomFixed(100f, WIDTH - 100f), RandomFixed(100f, HEIGHT - 100f)
}

pub fn RandomSpawnGridSafe() -> (fixed, fixed) {
    let cellX = ToFixed(RandomNumber(2, CELLCOUNT-2))
    let cellY = Fmath:ToFixed(RandomNumber(2, CELLCOUNT-2))

    let x, y = cellX*CELLSIZE_X+CELLSIZE_X/2f, cellY*CELLSIZE_Y+CELLSIZE_Y/2f

    let counter = 0
    while #GetEntitiesInRadius(x, y, 1f) > 0 {
        counter += 1
        let cellX = ToFixed(RandomNumber(2, CELLCOUNT-2))
        let cellY = Fmath:ToFixed(RandomNumber(2, CELLCOUNT-2))

        x, y = cellX*CELLSIZE_X+CELLSIZE_X/2f, cellY*CELLSIZE_Y+CELLSIZE_Y/2f
        if counter > 16 {
            return x, y
        }
    }

    return x, y
}

pub Ambushes = [
    fn(number diff) {
        let x, y = PlayerSafeSpawn(RandomSpawn)
        repeat 1+diff*2 {
            let mag = Fmath:RandomFixed(8f, 100f)
            let my, mx = Fmath:Sincos(Rangle())
            NewSuperMothership(x+mx*mag, y+my*mag, MothershipType.Pentagon, Rangle())
        }
    } 
]

pub RoundOne = [
    fn(number time, diff) {
        if time == 2 or time % 350 == 0 {
            let x, y = PlayerSafeSpawn(RandomSpawn)
            repeat (3*diff)\1 {
                NewMothership(x, y, MothershipType.Triangle, Rangle())
            }
        }
    },
    fn(number time, diff) {
        if time == 2 {
            let x, y = PlayerSafeSpawn(RandomSpawnGridSafe)
            spawn Pylon:Pylon(x, y)
        }
        if time % 440 == 0 {
            repeat diff-1 {
                let x, y = PlayerSafeSpawn(RandomSpawnGridSafe)
                spawn Pylon:Pylon(x, y)
            }
        }
    },
    fn(number time, diff) {
        if time == 2 {
            repeat 5+diff*2 {
                let x, y = PlayerSafeSpawn(RandomSpawn)
                NewRollingCube(x, y)
            }
        }
        if time % 200 == 0 {
            repeat 2+diff*2 {
                let x, y = PlayerSafeSpawn(RandomSpawn)
                NewRollingCube(x, y)
            }
        }
    }
]

pub RoundTwo = [
    fn(number time, diff) {
        if time == 2 or time % 440 == 0 {
            repeat 1+diff {
                let x, y = PlayerSafeSpawn(RandomSpawn)
                spawn HypercubeEntity:Hypercube(x, y)
            }
        }
    },
    fn(number time, diff) {
        if time == 2 or time % 300 == 0 {
            let x, y = PlayerSafeSpawn(RandomSpawn)
            NewInertiac(x, y, 1.5f, Rangle())
        }
    },
    fn(number time, diff) {
        if time == 100 or (time-100) % 400 == 0 {
            let dy, dx = Fmath:Sincos(Rangle())
            dx, dy *= 20f, 20f
            let x, y = PlayerSafeSpawn(RandomSpawn)
            let id = NewUFO(x+dx, y+dy, 3f)
            let id2 = NewUFO(x-dx, y-dy, 3f)
            SetUFOWallCollision(id, true)
            SetUFOWallCollision(id2, true)
        }
    },
    fn(number time, diff) {
        let bafAmount = ToFixed(diff)+3fx
        const BAF_SIZE = 10fx
        
        if time % 170 == 0 {
            let x, y = RandomWallSpawn(bafAmount*BAF_SIZE)
            fixed my, mx, py, px
            if x == 0 or x == WIDTH {
                my, mx, py, px = Sincos(90d), SIncos(270d)
            }else {
                my, mx, py, px = Sincos(0d), Sincos(180d)
            }
            NewBafBlue(x, y, direction, 7fx)
            repeat from 1fx to bafAmount {
                NewBafBlue(x+mx*BAF_SIZE, y+my*BAF_SIZE, direction, 7fx)
                NewBafBlue(x+px*BAF_SIZE, y+py*BAF_SIZE, direction, 7fx)
            }
        }
    },
    fn (number time, diff) {
        if time == 180 {
            let x, y = PlayerSafeSpawn(RandomSpawn)
            NewBonus(x, y, BonusType.Weapon, struct{
                box_duration = 300, 
                cannon = CannonType.DoubleSwipe,
                frequency = CannonFreq.Freq30,
                weapon_duration = 160,
            })
        }
    }
]

pub RoundThree = [
    fn(number time, diff) {
        if time == 2 or time % 600 == 0 {
            let x, y = PlayerSafeSpawn(RandomSpawn)
            spawn SuperInertiacEntity:SuperInertiac(x, y)
        }
    },
    fn(number time, diff) {
        if time == 2 or time % 300 == 0 {
            let bafAmount = 14+diff
            let angleStep = 360d / ToFixed(bafAmount)
            let x, y = PlayerSafeSpawn(RandomSpawn)
            repeat bafAmount with i {
                let angle = angleStep * ToFixed(i)
                let dy, dx = Sincos(angle)
                NewYellowBAF(x + dx * 80f, y + dy * 80f, angle, 10f, 500)
            }
        }
    },
    fn(number time, diff) {
        if time == 100 or time % 450 == 0 {
            let x, y = PlayerSafeSpawn(RandomSpawn)
            let crowderAmount = Fmath:ToFixed(7+diff)
            let r = 13f*crowderAmount
            repeat crowderAmount {
                let dy, dx = Sincos(Fmath:RandomFixed(0d, 360d))
                NewCrowder(x + dx * r, y + dy * r)
                r *= 0.8f
            }
        }
    }
]

pub RoundFour = [
    fn(number time, diff) {
        if time == 2 {
            repeat 3+diff {
                let x, y = PlayerSafeSpawn(RandomSpawnGridSafe)
                spawn VithenEntity:Vithen(x, y, diff)
            }
        } else if time % 400 == 0 {
            repeat 1+diff {
                let x, y = PlayerSafeSpawn(RandomSpawnGridSafe)
                spawn VithenEntity:Vithen(x, y, diff)
            }
        }
    },
    fn(number time, diff) {
        if time == 2 or time % 600 == 0 {
            let x, y = PlayerSafeSpawn(RandomSpawn)
            spawn WormholeEntity:Wormhole(x, y, 50f)
        }
    },
    fn(number time, diff) {
        if time % 200 == 0 {
            repeat 1+diff {
                let x, y = PlayerSafeSpawn(RandomSpawn)
                NewMothership(x, y, MothershipType.Pentagon, Rangle())
            }
        }
    },
    fn (number time, diff) {
        if time == 280 {
            let x, y = PlayerSafeSpawn(RandomSpawn)
            NewBonus(x, y, BonusType.Weapon, struct{
                box_duration = 300, 
                cannon = CannonType.Laser,
                weapon_duration = 160,
                frequency = CannonFreq.Freq15
            })
        }
    }
]